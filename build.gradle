plugins {
	id 'net.minecraftforge.gradle' version '5.1.+'
	id 'org.spongepowered.mixin' version '0.7-SNAPSHOT' // Mixins
	id 'org.parchmentmc.librarian.forgegradle' version '1.+'
	id 'io.github.CDAGaming.cursegradle' version '1.6.0'
	id 'java'
	id 'maven-publish'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
java.withSourcesJar()

archivesBaseName = "${mod_name}-forge-${minecraft_version}"

minecraft {
	mappings channel: mappings_channel, version: "${mappings_version}-${minecraft_version}"
	//accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

	runs {
		client {
			workingDirectory project.file('run')
			ideaModule "${rootProject.name}.${project.name}.main"
			taskName 'Client'
			property 'mixin.env.remapRefMap', 'true'
			property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
			args "-mixin.config="+mod_id+".mixins.json"

			mods {
				modClientRun {
					source sourceSets.main
				}
			}
		}

		server {
			workingDirectory project.file('run')
			ideaModule "${rootProject.name}.${project.name}.main"
			taskName 'Server'
			property 'mixin.env.remapRefMap', 'true'
			property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
			args "-mixin.config="+mod_id+".mixins.json"

			mods {
				modServerRun {
					source sourceSets.main
				}
			}
		}

		data {
			workingDirectory project.file('run')
			ideaModule "${rootProject.name}.${project.name}.main"
			taskName 'Data'
			property 'mixin.env.remapRefMap', 'true'
			property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
			args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/'), "-mixin.config="+mod_id+".mixins.json"

			mods {
				modDataRun {
					source sourceSets.main
				}
			}
		}
	}
}

sourceSets.main.resources.srcDir 'src/generated/resources'

mixin { // Add mixins refmap
	add sourceSets.main, "${mod_id}.refmap.json"
}

repositories {
	mavenCentral()

	maven {
		name = 'Sponge / Mixin'
		url = 'https://repo.spongepowered.org/repository/maven-public/'
	}

	maven {
		name = "Forge"
		url = 'https://maven.minecraftforge.net'
	}

	maven {
		name = "MidnightLib"
		url = "https://api.modrinth.com/maven"
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
}

tasks.withType(GenerateModuleMetadata) {
	enabled = false
}

dependencies {
	minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

	if (System.getProperty("idea.sync.active") != "true") {
		annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
	}
}

processResources {
	from sourceSets.main.resources
	inputs.property "version", project.version

	filesMatching("mods.toml") {
		expand "version": project.version
	}
}

jar {
	manifest {
		attributes([
				'Specification-Title'     : mod_name,
				'Specification-Vendor'    : mod_author,
				'Specification-Version'   : project.jar.archiveVersion,
				'Implementation-Title'    : project.name,
				'Implementation-Version'  : project.jar.archiveVersion,
				'Implementation-Vendor'   : mod_author,
				'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
				'Timestamp'              : System.currentTimeMillis(),
				'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
				'Built-On-Minecraft'      : minecraft_version,
				"MixinConfigs": "${mod_id}.mixins.json"
		])
	}
}

jar.finalizedBy('reobfJar')

publishing {
	repositories {
		maven {
			name = "Cloudsmith"
			url 'https://maven.cloudsmith.io/tslat/tes/'
			credentials {
				username System.getenv("CLOUDSMITH_USERNAME")
				password System.getenv("CLOUDSMITH_API_KEY")
			}
		}
	}

	publications {
		maven(MavenPublication) {
			artifactId = archivesBaseName
			artifact jar
			artifact sourcesJar
		}
	}
}

curseforge {
	apiKey = System.getenv("CURSEFORGE_TOKEN")

	project {
		id = curseforge_id
		changelog = 'https://github.com/Tslat/TslatEntityStatus/commits/1.16.5'
		releaseType = 'release'
		addGameVersion minecraft_version

		mainArtifact(jar) {
			it.displayName = "${project.mod_name} Forge ${project.minecraft_version}-${project.version}"
		}
	}
}